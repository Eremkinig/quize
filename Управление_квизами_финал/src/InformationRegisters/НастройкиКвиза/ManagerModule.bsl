#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура НовыйКвиз(ОписаниеКвиза) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	
	Для Каждого КомандаКвиза Из ОписаниеКвиза.Команды Цикл
		
		Для Каждого ВопросРаунда Из ОписаниеКвиза.ВопросыРаундов Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВопросРаунда);
			НоваяЗапись.Команда = КомандаКвиза.Команда;
			НоваяЗапись.Квиз = ОписаниеКвиза.Квиз;
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Параметры - Квиз - ОпределяемыйТип.Квиз
//
// Возвращаемое значение - Структура
//  * Квиз - ОпределяемыйТип.Квиз
//  * Раунды - ТаблицаЗначений
//     * Раунд - Строка
//     * Вопрос - СправочникСсылка.Вопросы
//     * ЭталонныйОтвет - ОпределяемыйТип.Ответ
//     * КоличествоБаллов - Число
//     * ФорматОтвета - ОписаниеТипов
//   * Команды - ТаблицаЗначений
//     * Команда - СправочникСсылка.Команды
//
Функция НастройкиКвиза(Квиз) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиКвиза.Квиз КАК Квиз,
	|	НастройкиКвиза.Команда КАК Команда,
	|	НастройкиКвиза.Раунд КАК Раунд,
	|	НастройкиКвиза.Вопрос КАК Вопрос,
	|	НастройкиКвиза.ЭталонныйОтвет КАК ЭталонныйОтвет,
	|	НастройкиКвиза.КоличествоБаллов КАК КоличествоБаллов
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	РегистрСведений.НастройкиКвиза КАК НастройкиКвиза
	|ГДЕ
	|	НастройкиКвиза.Квиз = &Квиз
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Раунд КАК Раунд,
	|	ВТ_Настройки.Вопрос КАК Вопрос,
	|	ВТ_Настройки.ЭталонныйОтвет КАК ЭталонныйОтвет,
	|	ВТ_Настройки.КоличествоБаллов КАК КоличествоБаллов,
	|	НЕОПРЕДЕЛЕНО КАК ФорматОтвета
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Настройки.Вопрос,
	|	ВТ_Настройки.ЭталонныйОтвет,
	|	ВТ_Настройки.КоличествоБаллов,
	|	ВТ_Настройки.Раунд
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Настройки.Раунд.ПорядковыйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Команда КАК Команда
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Настройки.Команда";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	НастройкиРаундов = РезультатЗапроса[1].Выгрузить();
	Для Каждого ВопросРаунда Из НастройкиРаундов Цикл
		СтроковоееПредставлениеТипа = ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗнч(ВопросРаунда.ЭталонныйОтвет));
		ВопросРаунда.ФорматОтвета = Новый ОписаниеТипов(СтроковоееПредставлениеТипа);
	КонецЦикла;
	
	НастройкиКоманд = РезультатЗапроса[2].Выгрузить();
	
	Результат = Новый Структура;
	Результат.Вставить("Раунды", НастройкиРаундов);
	Результат.Вставить("Команды", НастройкиКоманд);
	
	Возврат Результат;
	
КонецФункции

Функция КомандаКапитана(Квиз) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиКвиза.Команда КАК Команда
	|ИЗ
	|	РегистрСведений.НастройкиКвиза КАК НастройкиКвиза
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Команды КАК Команды
	|		ПО НастройкиКвиза.Команда = Команды.Ссылка
	|ГДЕ
	|	НастройкиКвиза.Квиз = &Квиз
	|	И Команды.Капитан = &Капитан";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	
	КапитанКоманды = Пользователи.ТекущийПользователь();
	Запрос.УстановитьПараметр("Капитан", КапитанКоманды);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Команда = Справочники.Команды.ПустаяСсылка();
	Если Выборка.Следующий() Тогда
		Команда = Выборка.Команда;
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru='Для капитана ""%1"" не удалось определить команду.
																|Обратитесь к ведущему квиза'"), КапитанКоманды);
	КонецЕсли;
	
	Возврат Команда;
	
КонецФункции

// Параметры 
//  - Квиз - ОпределяемыйТип.Квиз
//
// Возвращаемое значение - ТаблицаЗначений
//    * Раунд - Строка
//    * Вопрос - СправочникСсылка.Вопросы
//     * ФорматОтвета - ОписаниеТипов
//
Функция ВопросыКвиза(Квиз) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиКвиза.Раунд КАК Раунд,
	|	НастройкиКвиза.Вопрос КАК Вопрос,
	|	НЕОПРЕДЕЛЕНО КАК ФорматОтвета,
	|	НастройкиКвиза.ЭталонныйОтвет КАК ЭталонныйОтвет
	|ИЗ
	|	РегистрСведений.НастройкиКвиза КАК НастройкиКвиза
	|ГДЕ
	|	НастройкиКвиза.Квиз = &Квиз
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиКвиза.Раунд,
	|	НастройкиКвиза.Вопрос,
	|	НастройкиКвиза.ЭталонныйОтвет
	|
	|УПОРЯДОЧИТЬ ПО
	|	Раунд,
	|	Вопрос";
	
	Запрос.УстановитьПараметр("Квиз", Квиз);
	
	Вопросы = Запрос.Выполнить().Выгрузить();
	Результат = Вопросы.СкопироватьКолонки("Раунд, Вопрос, ФорматОтвета");
	
	Для Каждого ТекущийВопрос Из Вопросы Цикл
		
		НоваяСтрока = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущийВопрос);
		
		СтроковоеПредставлениеТипа = ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗнч(ТекущийВопрос.ЭталонныйОтвет));
		НоваяСтрока.ФорматОтвета = Новый ОписаниеТипов(СтроковоеПредставлениеТипа);
		
	КонецЦикла;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
