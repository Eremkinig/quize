
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура СоздатьНовыйКвиз() Экспорт
	
	ОписаниеКвиза = УправлениеКвизами.НовоеОписаниеКвиза();
	ОписаниеКвиза.Квиз = Квиз;
		
	Для Каждого ВопросРаунда Из НастройкиРаундов Цикл
		ОписаниеВопросаРаунда = УправлениеКвизами.НовоеОписаниеВопросаРаунда();
		ЗаполнитьЗначенияСвойств(ОписаниеВопросаРаунда, ВопросРаунда);
		ОписаниеКвиза.ВопросыРаундов.Добавить(ОписаниеВопросаРаунда);
	КонецЦикла;
	
	Для Каждого КомандаКвиза Из Команды Цикл
		ОписаниеКоманды = УправлениеКвизами.НовоеОписаниеКоманды();
		ЗаполнитьЗначенияСвойств(ОписаниеКоманды, КомандаКвиза);
		ОписаниеКвиза.Команды.Добавить(ОписаниеКоманды);
	КонецЦикла;
	
	УправлениеКвизами.НовыйКвиз(ОписаниеКвиза);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстОшибки = "";
	
	Если НастройкиРаундов.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru='Не настроены вопросы по раундам'");
	КонецЕсли;
	
	Если Команды.Количество() = 0 Тогда
		ТекстОшибки = ТекстОшибки + НСтр("ru='Не настроены команды'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
