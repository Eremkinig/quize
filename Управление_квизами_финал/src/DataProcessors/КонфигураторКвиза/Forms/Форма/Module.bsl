
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Квиз) Тогда
		ВызватьИсключение НСтр("ru = 'Настройку квиза можно осуществлять
														|только в форме объекта-владельца.'");
	КонецЕсли;
	
	Объект.Квиз = Параметры.Квиз;
	
	ЗагрузитьТекущиеНастройкиКвиза();
	
	УстановитьПараметрыСвязейВыбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаунды

&НаКлиенте
Процедура РаундыВопросПриИзменении(Элемент)
	
	ТекДанные = Элементы.Раунды.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Информация = ИнформацияПоВопросу(ТекДанные.Вопрос);
	ТекДанные.ЭталонныйОтвет = Информация.Ответ;
	ТекДанные.КоличествоБаллов = Информация.КоличествоБаллов;
	
	ФорматыОтвета = ДоступныеФорматыОтвета();
	Элементы.РаундыФорматОтвета.ДоступныеТипы = Новый ОписаниеТипов(ФорматыОтвета);
	
	ПредставлениеТипа = СтроковоеПредставлениеТипа(ТипЗнч(ТекДанные.ЭталонныйОтвет));
	ТекДанные.ФорматОтвета = Новый ОписаниеТипов(ПредставлениеТипа);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьТекущиеНастройкиКвиза()
	
	ТекущиеНастройки = УправлениеКвизами.НастройкиКвиза(Объект.Квиз);
	Объект.НастройкиРаундов.Загрузить(ТекущиеНастройки.Раунды);
	Объект.Команды.Загрузить(ТекущиеНастройки.Команды);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияПоВопросу(Знач Вопрос)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Вопрос, "Ответ, КоличествоБаллов");
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступныеФорматыОтвета()
	
	Возврат УправлениеКвизамиПовтИсп.ДоступныеФорматыОтвета();
	
КонецФункции

&НаСервереБезКонтекста
Функция СтроковоеПредставлениеТипа(Знач Тип)
	
	Возврат ОбщегоНазначения.СтроковоеПредставлениеТипа(Тип);
	
КонецФункции

&НаКлиенте
Процедура СоздатьКвиз(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		СоздатьНовыйКвиз();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНовыйКвиз()
	
	КонфигураторКвиза = РеквизитФормыВЗначение("Объект");
	КонфигураторКвиза.СоздатьНовыйКвиз();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСвязейВыбора()
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Квиз");
	ПараметрыВыбора = Новый Массив();
	ПараметрыВыбора.Добавить(НоваяСвязь);
	
	Элементы.РаундыРаунд.СвязиПараметровВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

#КонецОбласти
