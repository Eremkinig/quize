
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КвизПриИзменении(Элемент)
	
	НастроитьРабочееМестоКапитана(Объект.Квиз);
	
	ОтфильтроватьВопросыПоРаунду(Раунд);
	
КонецПроцедуры

&НаКлиенте
Процедура РаундПриИзменении(Элемент)
	
	ОтфильтроватьВопросыПоРаунду(Раунд);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьОтветы(Команда)
	
	ЗафиксироватьОтветыКоманды(Объект.Квиз, Объект.Команда, Раунд);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьРабочееМестоКапитана(Знач Квиз)
	
	Вопросы = УправлениеКвизами.ВопросыКвиза(Квиз);
	Объект.Ответы.Загрузить(Вопросы);
	
	УстановитьКоманду(Квиз);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоманду(Квиз)
	
	КомандаКапитана = УправлениеКвизами.КомандаКапитана(Квиз);
	Объект.Команда = КомандаКапитана;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтфильтроватьВопросыПоРаунду(Раунд)
	
	Если ЗначениеЗаполнено(Раунд) Тогда
		Отбор = Новый ФиксированнаяСтруктура("Раунд", Раунд);
	Иначе
		Отбор = Новый ФиксированнаяСтруктура("Раунд", Неопределено);
	КонецЕсли;
	
	Элементы.Ответы.ОтборСтрок = Отбор;
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьОтветыКоманды(Знач Квиз, Знач Команда, Знач Раунд)
	
	НовоеОписаниеОтветов = УправлениеКвизами.НовоеОписаниеОтветовКоманды();
	
	Отбор = Новый Структура("Раунд", Раунд);
	ОтветыРаунда = Объект.Ответы.НайтиСтроки(Отбор);
	Для Каждого Ответ Из ОтветыРаунда Цикл
		НоваяСтрока = НовоеОписаниеОтветов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Ответ);
		НоваяСтрока.Квиз = Квиз;
		НоваяСтрока.Команда = Команда;
	КонецЦикла;
	
	УправлениеКвизами.ЗафиксироватьОтветыРаунда(НовоеОписаниеОтветов);
	
КонецПроцедуры

#КонецОбласти


