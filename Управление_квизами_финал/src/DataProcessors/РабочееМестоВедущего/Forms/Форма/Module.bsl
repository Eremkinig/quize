#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КвизПриИзменении(Элемент)
	
	ОбновитьИнформациюПоОтветам(Квиз);
	ОбновитьДанныеПоРаундам(Квиз);
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтветыКоманд

&НаКлиенте
Процедура ОтветыКомандОтветПринятПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтветыКоманд.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = НовыйОтборДерева();
	ЗаполнитьЗначенияСвойств(Отбор, ТекущиеДанные);
	
	Если ЭтоВерхнийУровеньДерева(Отбор) Тогда
		
		ТекущиеДанные.ОтветПринят = ПредыдущееЗначениеПринятогоОтвета(ТекущиеДанные.ОтветПринят);
		
	Иначе
		
		Ответ = УправлениеКвизамиКлиент.НовоеОписаниеОтвета();
		ЗаполнитьЗначенияСвойств(Ответ, ТекущиеДанные);
		Ответ.Квиз = Квиз;
		
		Если ОтветБылОтправленКомандой(Ответ) Тогда
			ЗафиксироватьОтвет(Ответ, ТекущиеДанные.ОтветПринят);
		Иначе 
			ТекущиеДанные.ОтветПринят = ПредыдущееЗначениеПринятогоОтвета(ТекущиеДанные.ОтветПринят);
			ТекстСообщения = НСтр("ru='Команда не отправила ответ на данный вопрос'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаунды

&НаКлиенте
Процедура РаундыАктивенПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Раунды.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаундАктивен = ТекущиеДанные.Активен;
	Для Каждого Раунд Из Раунды Цикл
		Раунд.Активен = Ложь;
	КонецЦикла;
	ТекущиеДанные.Активен = РаундАктивен;
	
	АктивироватьРаунд(Квиз, ТекущиеДанные.Раунд);
	
	ОбновитьДанныеПоРаундам(Квиз);
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьРезультаты(Команда)
	
	ТекущиеДанные = Элементы.ОтветыКоманд.ТекущиеДанные;
	
	ОбновитьИнформациюПоОтветам(Квиз);

	ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(
																		"Вопрос", 
																		Элементы.ОтветыКоманд.ТекущаяСтрока,
																		ОтветыКоманд.ПолучитьЭлементы(),
																		ТекущиеДанные.Вопрос,
																		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьКвиз(Команда)
	
	АктивироватьРаунд(Квиз, Раунды[0].Раунд);
	
	ОбновитьДанныеПоРаундам(Квиз);
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьКвиз(Команда)
	
	ЗавершитьКвизНаСервере(Квиз);
	
	ОбновитьДанныеПоРаундам(Квиз);
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОтветБылОтправленКомандой(Знач Ответ)
	
	Возврат УправлениеКвизами.ОтветБылОтправленКомандой(Ответ);
	
КонецФункции

&НаКлиенте
Функция ПредыдущееЗначениеПринятогоОтвета(ОтветПринят)
	
	Возврат НЕ ОтветПринят;
	
КонецФункции

&НаКлиенте
Функция НовыйОтборДерева()
	
	Отбор = Новый Структура;
	Отбор.Вставить("Команда");
	Отбор.Вставить("Раунд");
	Отбор.Вставить("Вопрос");
	
	Возврат Отбор;
	
КонецФункции

&НаСервере
Функция ЭтоВерхнийУровеньДерева(Знач Отбор)
	
	ОтветыКомандОбъект = РеквизитФормыВЗначение("ОтветыКоманд");
	СтрокиДерева = ОтветыКомандОбъект.Строки.НайтиСтроки(Отбор);
	
	Если СтрокиДерева.Количество() Тогда
		Возврат СтрокиДерева[0].Уровень() = 0;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьИнформациюПоОтветам(Знач Квиз)
	
	ОтветыКомандИерархия = УправлениеКвизами.ИнформацияПоОтветамИерархически(Квиз);
	ЗначениеВРеквизитФормы(ОтветыКомандИерархия, "ОтветыКоманд");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоРаундам(Знач Квиз)
	
	ИнформацияПоРаундам = УправлениеКвизами.ИнформацияПоРаундамКвиза(Квиз);
	Раунды.Загрузить(ИнформацияПоРаундам);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗафиксироватьОтвет(Знач Ответ, Знач ОтветПринят)
	
	УправлениеКвизами.ЗафиксироватьОтвет(Ответ, ОтветПринят);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура АктивироватьРаунд(Знач Квиз, Знач Раунд)
	
	УправлениеКвизами.АктивироватьРаунд(Квиз, Раунд);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗавершитьКвизНаСервере(Знач Квиз)
	
	УправлениеКвизами.ЗавершитьКвиз(Квиз);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(Форма)
	
	ЕстьАктивныеРаунды = Форма.Раунды.НайтиСтроки(Новый Структура("Активен", Истина)).Количество();
	
	Форма.Элементы.НачатьКвиз.Доступность = НЕ ЕстьАктивныеРаунды;
	Форма.Элементы.ЗавершитьКвиз.Доступность = ЕстьАктивныеРаунды;
	
КонецПроцедуры

#КонецОбласти











